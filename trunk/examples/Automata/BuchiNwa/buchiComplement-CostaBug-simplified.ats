// 2015-07-30, Matthias Heizmann
// reveals bug in buchiComplementBS and buchiComplementFKV
// obtained from termination analysis of CostaDouble.bpl


NestedLassoWord nlw = [  "a" "a" , "c"< "c"< >"r" >"r" "a" "a" ];
boolean accepted = buchiAccepts(ia, nlw);
NestedWordAutomaton complementBS = buchiComplementBS(ia);
boolean acceptedComplementBS = buchiAccepts(complementBS, nlw);
NestedWordAutomaton complementFKV = buchiComplementFKV(ia);
boolean acceptedComplementFKV = buchiAccepts(complementFKV, nlw);
assert(!accepted);
assert(acceptedComplementBS);
assert(acceptedComplementFKV);



NestedWordAutomaton ia = (
	callAlphabet = {"c" },
	internalAlphabet = {"a" },
	returnAlphabet = {"r" },
	states = {"q" "f" "s" "t" },
	initialStates = {"q" },
	finalStates = {"f" },
	callTransitions = {
		 ("q" "c" "t")
		 ("t" "c" "t")
		 ("s" "c" "t")
	},
	internalTransitions = {
		 ("q" "a" "q")
		 ("q" "a" "f")
		 ("f" "a" "s")
		 ("s" "a" "s")
		 ("t" "a" "t")
	},
	returnTransitions = {
		 ("t" "q" "r" "q")
		 ("t" "t" "r" "t")
		 ("t" "s" "r" "s")
	}
);
