// 2015-08-17, Matthias Heizmann
// reveals bug in automata library BuchiComplementFKV 

NestedLassoWord nlw = [  "c"< >"r1" "a" "c"<  , "c"< "c"< >"r2" >"r1" "a" "c"< ];
boolean accepted = buchiAccepts(ia, nlw);
NestedWordAutomaton complementBS = buchiComplementBS(ia);
boolean acceptedComplementBS = buchiAccepts(complementBS, nlw);
NestedWordAutomaton complementFKV = buchiComplementFKV(ia);
boolean acceptedComplementFKV = buchiAccepts(complementFKV, nlw);
assert(!accepted);
assert(acceptedComplementBS);
assert(acceptedComplementFKV);
print(complementFKV);


NestedWordAutomaton ia = (
	callAlphabet = {"c" },
	internalAlphabet = {"a" },
	returnAlphabet = {"r1" "r2" },
	states = {"f" "s" "q" },
	initialStates = {"q" },
	finalStates = {"f" },
	callTransitions = {
		 ("s" "c" "s")
		 ("q" "c" "q")
	},
	internalTransitions = {
		 ("q" "a" "q")
		 ("s" "a" "s")
		 ("f" "a" "s")
	},
	returnTransitions = {
		 ("s" "s" "r1" "s")
		 ("q" "q" "r1" "q")
		 ("q" "q" "r1" "f")
		 ("s" "s" "r2" "s")
		 ("q" "q" "r2" "q")

	}
);
