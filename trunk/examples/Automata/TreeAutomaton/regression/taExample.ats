
// Tree automaton that accepts lists of natural numbers in unary encoding
TreeAutomaton taLists = (
    alphabet = { nil "0" cons succ elem },
    states = { Node Num List "_" },
    initialStates = { "_" },
    finalStates = { List },
    transitionTable = {
        (("_") "0" Num)
        ((Node List) cons List)
        (("_") nil List)
        ((Num) succ Num)
        ((Num) elem Node)
    }
);

// [0, 1]
Tree list = Tree[cons(elem("0"), cons(elem(succ("0")), nil))];

// Good list
assert(accepts(taLists, list));

// _____________________________________________________________________________



// Tree automaton that accepts binary trees whose nodes contain natural numbers 
// in unary encoding
TreeAutomaton taBinaryTrees = (
    alphabet = { nil "0" cons succ elem },
    states = { Node Num BinTree "_" },
    initialStates = { "_" },
    finalStates = { BinTree },
    transitionTable = {
        (("_") "0" Num)
        ((Node BinTree BinTree) cons BinTree)
        ((Node BinTree) cons BinTree)
        (("_") nil BinTree)
        ((Num) succ Num)
        ((Num) elem Node)
    }
);


//       1
//     /   \
//    0     -
//   /
//  -
Tree binaryTree = Tree[cons(elem(succ("0")), cons(elem("0"), nil), nil)];

// Good binary tree
assert(accepts(taBinaryTrees, binaryTree));


// _____________________________________________________________________________


// Check that our example list is also a binary tree
assert(accepts(taBinaryTrees, list));

// Check that all lists accepted by tree automaton taLists are also accepted by 
// tree automaton taBinaryTrees.
// (checks set inclusion by reduction to emptiness, intersection and complement 
//  operations)
assert(TreeEmptinessCheck(Intersect(taLists, Complement(taBinaryTrees))));

